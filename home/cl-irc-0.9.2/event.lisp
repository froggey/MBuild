;;;; $Id: event.lisp 243 2013-07-01 20:16:35Z jdanjou $
;;;; $URL: file:///project/cl-irc/svn/tags/0.9.2/event.lisp $

;;;; See LICENSE for licensing information.

(in-package :irc)

(defgeneric irc-message-event (connection message)
  (:documentation "Upon receipt of an IRC message from the
connection's stream, irc-message-event will be called with the
message."))

(defmethod irc-message-event (connection (message irc-message))
  (declare (ignore connection))
  (unless (apply-to-hooks message)
    (client-log (connection message) message "UNHANDLED-EVENT:")))

(defgeneric dcc-message-event (connection message)
  (:documentation "Upon receipt of an IRC message from the
connection's stream, irc-message-event will be called with the
message."))

(defmethod dcc-message-event (connection (message dcc-message))
  (declare (ignore connection))
  (unless (apply-to-hooks message)
    (client-log (connection message) message "UNHANDLED-EVENT:")))


(defgeneric default-hook (message)
  (:documentation "Minimum action to be executed upon reception
of the IRC message to keep the connection, channel and user
objects in sync."))

(defmacro generate-maskmode-hooks (listmsg-class endmsg-class
                                                 tmp-symbol mode-symbol)
  `(progn
     (defmethod default-hook ((message ,listmsg-class))
       (destructuring-bind
           (target channel-name mask &optional set-by time-set)
           (arguments message)
         (declare (ignore target set-by time-set))
         ;; note: the structure currently does not allow for logging
         ;; set-by and time-set: the MODE message handling currently
         ;; does not allow that.
         (let ((channel (find-channel (connection message) channel-name)))
           (when channel
             (unless (has-mode-p channel ',tmp-symbol)
               ;; start with a new list, replacing the old value later
               (add-mode channel ',tmp-symbol
                         (make-instance 'list-value-mode
                                        :value-type :non-user)))
             ;; use package-local symbol to prevent conflicts
             (set-mode channel ',tmp-symbol mask)))))

     (defmethod default-hook ((message ,endmsg-class))
       (let ((channel (find-channel (connection message)
                                    (car (arguments message)))))
         (when channel
           (let ((mode (has-mode-p channel ',tmp-symbol)))
             (when mode
               ;; replace list
               (add-mode channel ',mode-symbol mode)
               (remove-mode channel ',tmp-symbol))))))))

(generate-maskmode-hooks irc-rpl_banlist-message
                         irc-rpl_endofbanlist-message
                         banlist-in-progress :ban)
(generate-maskmode-hooks irc-rpl_exceptlist-message
                         irc-rpl_endofexceptlist-message
                         exceptlist-in-progress :except)
(generate-maskmode-hooks irc-rpl_invitelist-message
                         irc-rpl_endofinvitelist-message
                         invitelist-in-progress :invite)

(defmethod default-hook ((message irc-rpl_isupport-message))
  (destructuring-bind
      (target &rest capabilities)
      ;; the last argument contains only an explanitory text
      (butlast (arguments message))
    (declare (ignore target))
    (let* ((connection (connection message))
           (current-case-mapping (case-map-name connection)))
      (flet ((split-arg (x)
                (let ((eq-pos (position #\= x)))
                  (if eq-pos
                      (list (substring x 0 eq-pos)
                            (substring x (1+ eq-pos)))
                    (list x))))
             (decode-arg (text)
                ;; decode \xHH into (char-code HH)
                ;; btw: how should that work with multibyte utf8?
                (format nil "窿ㄤ铼è篝狎ǐ痫螬痫箦狎汨④茗翦舂箦狎汨④茗翦后翎螋ū痫螬┅痫轭趔┅è铛祆痫螬蝈鲥蝮瘐箬篚怏趄轭翦篝狎舂痫轭趔┅瘐箬篚怏趄轭翦篝狎痫螬痫轭趔瘐箬ㄣ镤瀛汨狎疳蝮瀛轭翦珏翦后翎螋ǐ痫螬哄钿ǐ痫螬宏躅氕犰祜麇铋候徜轼倍┅痫轭趔┅┅铄玑翦疳蜥疳蜥愆ㄩㄥ＼ㄣ栳ㄦ轵篝疳蜥愆癌ㄡ篌镢篚怏趄轭ㄦ轵篝疳蜥愆暴溴驷蹯舡轶躔痫螋鲠祯弩呼弩＇篝蜷铉僵疳蜥愆┅箦翩箦蝣弪汜疳忾扉糸弩泔铑邈糸镱蝈漉沐＇灬礅溽ㄡ潢镩弘妁＇骈蝮呼弩＇篝蜷铉僵ㄡ痧孱蝈盹鲥铋磲疸狎＇铄玑翦疳蜥磲疸狎＇灬礅溽磲疸狎＇溴泔溴狎┅磲疸狎＇箴扉舡狎汜疳忾扉糸弩┅┅箦蝣弪汜疳忾扉糸弩泔铑邈糸镱┅洪铋糸犰鲠祯Ж┅箦翩ㄣ栳铑屐盹溴溴筱蜷痿轱铙泔铑邈糸镱ㄣ栳铐镤瀛溴筱蟓骝镯轶躔痫螋箦蝣弪汜疳忾扉糸弩泔铑邈糸镱┅铋汶痱彐轼弩泔铑邈糸镱铋汶痱彐轼弩骝镯轶躔痫螋箦蝣弪汜疳忾扉糸弩泔铑邈糸镱┅麒孱铒ㄥ聃犰沲蝌孱舡汜箦磲痧轭ㄣ狍瀛磲瓠钺礤泔铑邈糸镱┅换麇铄邃麸蝈铒蝽犰辁铋汶犷汨犷铄钺礤蝈狃痨汜箦磲痧轭泔铑邈糸镱┅┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝麒镩篚箦颦礤篌徵濠ㄤ弩趄蹉趱蜷铉忾钿翎蜱弭铋汶躞弪钺礤栾篝钺礤篝狎蝈犰钺礤ㄡ蜱蹴孱趔礤篌徵濠ㄤ邈灬蝈ㄩ珙矧翎蜱弭篝狎┅戾è躞弪ㄦ轭洵躞弪ㄣ镱铄泗轱礤篌徵濠铋汶┅麒孱躞弪箦翩蝈犰钺礤躞弪蝈犰钺礤躞弪钺礤躞弪躞弪钺礤ㄨ矬纛犴躞弪栾篝钺礤┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝麇煦镯瀛礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱栾篝躞弪狎珲礤铘螬礤篌徵ㄤ弩趄蹉趱蜷铉忾钿铋汶钺礤麇煦镯瀛礤篌徵濠狎珲礤铘箦翩躞弪泔铑邈糸镱磲脲躞弪泔铑邈糸镱侯殂腩犴铋汶钺礤鸿矬纛犴栾篝乎箦蝾犴躞弪┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝扉篝礤篌徵濠ㄤ弩趄蹉趱蜷铉忾钿ㄣ栳铑屐泔躅麸痖悌ㄡ蜱蹴孱趔礤篌徵濠戾è泔铑邈糸镱ㄣ镱铄泗轱礤篌徵濠躞弪泔躅疳蝮瀛轭翦珏泔躅舂┅ㄡ滗汨犷铄泔铑邈糸镱矧ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑磲脲汨犷铄泔铑邈糸镱侯犴汨犷铄呼镳殂麸痖乎箦颦泔躅躞弪泔躅舂┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝麸痖悱礤篌徵濠ㄤ弩趄蹉趱蜷铉忾钿翎蜱弭汨犷铄镳糸镱犰麸痖悌ㄡ蜱蹴孱趔礤篌徵濠ㄤ邈灬蝈ㄩ珙矧翎蜱弭┅箦翩麸痖ㄦ轭洵汨犷铄ㄣ镱铄泗轱礤篌徵濠汨犷铄飑麸痖悌┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝钺眚屦禊礤篌徵濠戾舄è泔铑邈糸镱ㄣ镱铄泗轱礤篌徵濠┅ㄤ弩趄蹉趱蜷铉忾钿铋汶汨犷鲩箝忾扉豉汨犷铄钺礤螬ㄡ蜱蹴孱趔礤篌徵濠ㄤ邈灬蝈ㄩ珙矧铋汶┅戾è汨犷铄ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑┅箦翩鲩箝忾扉豉汨犷铄飑矧箦泔钿ㄡ篌镢汨犷鲩箝忾扉豉Жá舰吼踱扉悌á吼蜷鲠翦á愧后邈蝈舂呼弩＇篝蜷铉僵乎铍铒黝┅躅戾篌ㄨ狍盹溴汨犷铄ь犴蝈痨轭痱镧蝈篌ㄡ滗盹溴汨犷铄ь犴蝈痨轭痱镧蝈篌磲脲轭篝犷沐ъ轶舡鲠祯瀛盹溴忽犰蹂豉疱乎箦颟┅ㄤ镬轶铋汶钺礤麸脲铋瀛篝蜷铉钺礤螬戾è躞弪ㄦ轭洵矧磲脲躞弪泔铑邈糸镱ㄣ犷镱殂犰辁瀛铋汶钺礤泔铑邈糸镱铋汶钺礤┅┅躅戾篌ㄥ聃犰躞弪躞弪泔铑邈糸镱┅ㄡ滗躞弪泔铑邈糸镱躞弪ㄡ滗躞弪汨犷铄躞弪┅箦舡盹溴汨犷铄ь犴蝈痨轭痱镧蝈篌躞弪戾舄è盹溴汨狎ㄧ弭铋汶痱彐轼弩泔铑邈糸镱ㄥ祠铋汶钺礤癌┅盹溴钺礤麒孱盹溴汨狎盹溴钺礤骝镯汨狎泔铑邈糸镱汨犷铄盹溴汨狎┅┅麒孱盹溴钺礤ㄩㄨ狍盹溴汨犷铄盹溴钺礤箦舡盹溴汨犷铄盹溴钺礤躞弪箦舡盹溴鲠祯ㄡ滗盹溴汨犷铄盹溴钺礤磲脲盹溴泔铑邈糸镱汨犷铄盹溴钺礤┅躞弪┅┅┅┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝孱滹骖犴弩礤篌徵濠戾舄è汨犷铄ㄦ轭洵汨犷铄ㄣ镱铄泗轱礤篌徵濠箦泔钿ㄡ蜱蹴孱趔礤篌徵濠┅盹溴ㄧ弭盹溴汨犷铄ь犴蝈痨轭痱镧蝈篌┅ㄣ栳铑屐躞弪螬蝈盹鲥盹溴汨犷铄ь犴蝈痨轭痱镧蝈篌磲痂狍＇灬礅溽铋汶躞弪镡戛ㄤ邈灬蝈ㄩ珙矧铋汶┅瘐箬铄躞弪镡汨犷铄飙躞弪螬躞弪汨犷铄飑ㄤ镬轶躞弪蝈盹鲥殒＇灬礅溽礤礅弪盹溴┅汨犷铄飙躞弪螬蝈盹鲥躞弪汨犷铄躞弪┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱痖铉礤篌徵濠ㄡ痧禊＇痫铉ㄣ镱铄泗轱礤篌徵濠ㄡ蜱蹴孱趔礤篌徵濠┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱觑轭礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱箫躜沐栾篝躞弪狎珲礤铘螬礤篌徵ㄤ弩趄蹉趱蜷铉忾钿ㄣ栳铑屐狎珲礤铘戾è躞弪ㄦ轭洵矧磲脲躞弪泔铑邈糸镱箫躜沐鸿矬纛犴栾篝乎箦蝾犴躞弪┅ㄣ栳铑屐矧ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑磲脲汨犷铄泔铑邈糸镱侯犴汨犷铄飑┅麒孱箦戽礤篌徵瀛礤篌徵濠ㄡ滗汨犷铄泔铑邈糸镱汨犷铄飑ㄡ滗躞弪泔铑邈糸镱躞弪ㄡ滗躞弪汨犷铄躞弪┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱麸痖悱礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱狎珲礤铘螬礤篌徵ㄤ弩趄蹉趱蜷铉忾钿ㄣ栳铑屐镳糸镱犰麸痖悌狎珲礤铘箦翩麸痖ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑麸痖悌┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱疳螋礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱狎珲礤铘箫躜沐礤篌徵ㄤ弩趄蹉趱蜷铉忾钿ㄣ栳铑屐镳糸镱犰翦舂狎珲礤铘ㄤ邈灬蝈ㄩ珙矧翦舂戾è汨犷铄ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑躞弪ㄦ轭洵躞弪泔铑邈糸镱箫躜沐┅麒孱ㄡ钿躞弪汨犷铄飑ㄩ箦戽礤篌徵瀛礤篌徵濠蝈盹鲥汨犷铄躞弪汨犷铄飑蝈盹鲥躞弪汨犷铄躞弪┅┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱聃轸礤篌徵濠戾舄è泔铑邈糸镱ㄣ镱铄泗轱礤篌徵濠躞弪ㄦ轭洵躞弪泔铑邈糸镱箫躜沐礤篌徵濠┅躅戾篌铛祆躞弪蝈盹鲥躞弪弼弪麒弪泔铑邈糸镱躞弪┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱蝠爝汨犷铄祉镤彘蟓礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱狎珲礤铘螬礤篌徵ㄤ弩趄蹉趱蜷铉忾钿翎蜱弭汨犷铄蝈篝盹溴狎珲礤铘螬狎珲礤铘戾è汨犷铄ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑┅麒孱汨犷铄ㄡ痧禊盹溴汨犷珏泔铑邈糸镱汨犷铄盹溴狎珲礤铘躞弪泔铑邈糸镱┅┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱盹溴礤篌徵濠ㄤ弩趄蹉趱蜷铉忾钿翎蜱弭蝈篝狎珲礤铘螬ㄡ蜱蹴孱趔礤篌徵濠戾舄è泔铑邈糸镱ㄣ镱铄泗轱礤篌徵濠翎蜱弭矧ㄦ轭洵汨犷铄泔铑邈糸镱翎蜱弭ㄦ轭洵躞弪泔铑邈糸镱翎蜱弭┅┅麒孱翎蜱弭ㄡ痧禊盹溴汨犷珏泔铑邈糸镱翎蜱弭狎珲礤铘躞弪泔铑邈糸镱┅┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱铋汶礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱箫躜沐栾篝躞弪狎珲礤铘螬礤篌徵ㄤ弩趄蹉趱蜷铉忾钿铄鳝铋汶狎珲礤铘戾舄è躞弪ㄦ轭洵矧磲脲躞弪泔铑邈糸镱箫躜沐鸿矬纛犴栾篝乎箦蝾犴躞弪┅ㄣ栳铉瀛铋汶钺礤泔铑邈糸镱躞弪铄鳝铋汶┅┅ㄤ彐礤翳镤溴驷蹯舡栾镫è礤篌徵轵悱腴汶礤篌徵濠鏖翳箪雉ㄣ镱铄泗轱狎珲礤铘螬礤篌徵ㄤ弩趄蹉趱蜷铉忾钿ㄣ栳铑屐铋汶镳糸镱犰蝈狍镱狎珲礤铘ㄤ邈灬蝈ㄩ珙矧蝈狍镱┅戾舄è汨犷铄ㄦ轭洵汨犷铄泔铑邈糸镱汨犷铄飑躞弪ㄦ轭洵躞弪泔铑邈糸镱铋汶┅麒孱ㄡ钿躞弪汨犷铄飑ㄩ躞弪羼礤泔铑邈糸镱躞弪蝈盹鲥汨犷铄躞弪汨犷铄飑蝈盹鲥躞弪汨犷铄躞弪┅┅┅换＃Ｔ夏虾珏铄蜥翦翳弩蝈箴镱箦轭拿萌猎泔铘屮麸锂磲泸镬弭è溴骈铄泗沭蝈痨栾镫è礤篌徵瀛鲠礤篌徵瀛豉疱怙澌怙澌啜溴骓弭栾溴驷蹯舡栾镫è礤篌徵瀛鲠礤篌徵瀛豉疱┅麒孱ㄣ翥瓠蝈聃弩舡礤篌徵瀛鲠颟棱镤┅┅ㄤ彐轭瀛泗沭蝈痨栾镫礤篌徵泗沭糸礤礤篌徵濠眭祠轲戾鲠祯瀛忾钿箦泔钿黹铛翦栾躜溽翦盹铘遽溽ㄧ弭溴泔溴洵糸礤箦钿轵悱礤篌徵ㄣ镱铄泗轱礤篌徵濠侯雉殂箫躜沐礤篌徵濠磲脲泗沭礤篌徵ㄦ矧磲铋⒃赏立磲脲糸礤礤篌徵箦泔钿黹铛翦栾躜溽翦盹铘遽溽┅┅┅ㄤ彐轭瀛泗沭蝈痨栾镫礤篌徵泗沭箫躜沐礤篌徵濠箦钿轵悱礤篌徵ㄣ镱铄泗轱礤篌徵濠侯雉殂箫躜沐礤篌徵濠磲脲泗沭礤篌徵ㄦ矧磲铋⒂险颐梁梁立滹黝祜徜栾篝滹黝祜徜溟蝈泗矧滹黝祜徜骈戾┅┅ㄤ彐轭瀛泗沭蝈痨栾镫礤篌徵泗沭骈铉弪礤篌徵濠戾舄è躞弪躞弪ㄣ镱铄泗轱礤篌徵濠┅ㄦ轭珏颦轭骘ㄩ铒弪镳戾铉翳蝈犰钺礤躞弪┅┅蝈犰钺礤躞弪铋汶钺礤躞弪┅┅箦钿轵悱礤篌徵ㄣ镱铄泗轱礤篌徵濠侯雉殂箫躜沐礤篌徵濠磲脲泗沭礤篌徵ㄦ矧磲铋⑵晌桥立骈铉弪轭骘┅┅ㄤ彐轭瀛泗沭蝈痨栾镫礤篌徵泗沭鲥蝮轱瞽礤篌徵濠箦钿轵悱礤篌徵ㄣ镱铄泗轱礤篌徵濠侯雉殂箫躜沐礤篌徵濠磲脲泗沭礤篌徵ㄦ矧磲铋⒅乓由衔立泗沭鲥蝮轱瞠┅┅ㄤ彐轭瀛泗沭蝈痨栾镫礤篌徵泗沭痖铉礤篌徵濠箦钿轵悱礤篌徵ㄣ镱铄泗轱礤篌徵濠侯雉殂箫躜沐礤篌徵濠磲脲泗沭礤篌徵ㄦ矧磲铋⑿晌立ㄣ狎灬篝ㄡ蜱蹴孱趔礤篌徵濠┅┅┅ㄤ彐礤翳镤轵悱礤篌徵瀛弼孱ㄣ镱铄泗轱礤篌徵泗沭溷悱汨狒蝈聃弩舡礤篌徵濠ㄤ邈灬蝈ㄩ珙矧泔铑邈糸镱┅ㄡ痧禊麸栾镫礤篌徵濠ㄣ扉孱舡祜ㄣ镱铄泗轱礤篌徵濠礤篌徵濠麒孱ㄡ豸镯狒殂犰禊徙沐痿溷悱泔铑邈糸镱ㄣ镱骈珲蜥糸镱ㄣ镱铄泗轱礤篌徵濠┅戾舄è躞弪ㄦ轭洵躞弪ㄣ镱铄泗轱礤篌徵濠箫躜沐礤篌徵濠┅ㄡ蜱麸脲铋瀛篝蜷铉趄衢扉铉狎珲礤铘礤篌徵濠┅蝈盹翦徜潋弩ㄨ怙麸滹趑邃聃徜疳蝮瀛轭翦珏ㄦ秕螋狎珞┅┅蝈盹翦痫螋疳蝮瀛轭翦珏ㄦ殒翳狎珞宏躅氕犰祜麇舂┅瘐箬磲脲溷悱泔铑邈糸镱乎箦躞弪候屙雉瀛徜潋弩蝈盹翦徜潋弩候屙雉瀛痫螋蝈盹翦痫螋溷悱泔铑邈糸镱螵┅┅ㄤ彐礤翳镤轵悱礤篌徵瀛弼孱ㄣ镱铄泗轱礤篌徵泗沭溷悱箦钿蝈聃弩舡礤篌徵濠ㄤ邈灬蝈ㄩ珙矧泔铑邈糸镱┅ㄡ痧禊麸栾镫礤篌徵濠ㄣ扉孱舡祜ㄣ镱铄泗轱礤篌徵濠礤篌徵濠麒孱ㄡ豸镯狒殂犰禊徙沐痿溷悱滹黝祜徜ㄣ镱骈珲蜥糸镱ㄣ镱铄泗轱礤篌徵濠┅戾舄è躞弪ㄦ轭洵躞弪ㄣ镱铄泗轱礤篌徵濠箫躜沐礤篌徵濠┅ㄡ蜱麸脲铋瀛篝蜷铉趄衢扉铉狎珲礤铘礤篌徵濠┅ㄦ殪孱犴翳轵狎珞┅蝈盹翦徜潋弩ㄨ怙麸滹趑邃聃徜疳蝮瀛轭翦珏ㄦ秕螋狎珞┅┅蝈盹翦痫螋疳蝮瀛轭翦珏ㄦ殒翳狎珞┅ㄦ殪弩辁疳蝮瀛轭翦珏箝翳狎珞宏躅氕犰祜麇舂┅戾è溷悱泔铑邈糸镱磲脲溷悱泔铑邈糸镱乎箦躞弪候屙雉瀛徜潋弩蝈盹翦徜潋弩候屙雉瀛痫螋蝈盹翦痫螋┅鏖翳镳孱骈戾篝蝈犴骈戾钺礤轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴黩轸瀛箦聃孱沐蝈徜礤篌徵瀛祜镳溷悱泔铑邈糸镱篝蝈犴┅┅┅